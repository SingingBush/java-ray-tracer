import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

allprojects {
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    group = 'ex02'
    version = '1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.0'
    }

    test {
        useJUnitPlatform {
            // excludeTags 'ignore'
            includeEngines 'junit-jupiter'
            excludeEngines 'junit-vintage'
        }
        testLogging {
            events TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED
            showCauses true
        }
    }
}

javafx {
    version = JavaVersion.current().getMajorVersion()
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
}


mainClassName = "ex02.RayTracerFX"
//mainClassName = "ex02.RayTracerSWT"

if (Os.isFamily(Os.FAMILY_MAC)) {
    applicationDefaultJvmArgs = ["-XstartOnFirstThread"] // required on OSX
}

if (Os.isFamily(Os.FAMILY_UNIX)) {
    // JavaFX won't work properly with Wayland yet, even if you set GTK to version 2 with '-Djdk.gtk.version=2'
    // This can be worked around by setting an environment variable: GDK_BACKEND=x11
    run.environment('GDK_BACKEND', 'x11')
}

String os = System.getProperty('os.name').toLowerCase().split()[0]
String arch = System.getProperty('os.arch')

println "Need to build with native swt lib for ${os}-${arch}"

dependencies {
//    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation files("src/main/lib/swt-4.6.3-${os}.jar")

    // implementation 'org.openjfx:javafx-base:11'
    // implementation 'org.openjfx:javafx-controls:11'
    // implementation 'org.openjfx:javafx-fxml:11'

    implementation 'org.jetbrains:annotations:20.1.0'

    implementation project(':blas')
    implementation project(':entities')
    implementation project(':raytracer')

    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.2'
}
